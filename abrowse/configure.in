# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas
 
dnl Process this file with autoconf to produce a configure script.
 
AC_INIT(acinclude.m4) dnl a source file from your sub dir
 
dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)
 
AC_CANONICAL_SYSTEM
AC_ARG_PROGRAM
 
dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(konqueror-embedded, 0.1) dnl searches for some needed programs
 
KDE_SET_PREFIX
 
dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done
 
dnl Checks for programs.
AC_CHECK_COMPILERS
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
KDE_PROG_LIBTOOL
 
dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files
AM_KDE_WITH_NLS

AC_ARG_ENABLE(qt-embedded,[  --enable-qt-embedded    Enable Qt Embedded support (adds -fno-rtti and -DQWS)],
            want_qte=$enableval,want_qte=no)

if test "x$want_qte" = "xyes"; then
    CXXFLAGS="$CXXFLAGS -fno-rtti -DQWS"
    LDFLAGS="$LDFLAGS -fno-rtti"
fi

dnl We don't require an installed kdelibs (Simon) 
dnl AC_PATH_KDE
dnl Just Qt
AC_PATH_QT
AC_SUBST(all_libraries)
AC_SUBST(all_includes)

dnl ### hack for kdesrc/khtml/css/Makefile.am
kde_datadir='${prefix}/share/apps'
AC_SUBST(kde_datadir)
 
dnl Perform program name transformation
AC_ARG_PROGRAM                                                                                                                        

dnl Image readers/writers
dnl No need for that stuff (Simon)
dnl KDE_CHECK_KIMGIO

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h crypt.h \
	sys/select.h sys/ioctl.h sys/stropts.h stropts.h termio.h termios.h lastlog.h \
	sys/sockio.h krb5/krb5.h rpc/rpc.h rpc/key_prot.h \
	sys/m_wait.h ncurses.h paths.h login_cap.h syslog.h \
	sys/wait.h sys/ucred.h sys/mount.h floatingpoint.h fstab.h mntent.h \
	Alib.h libutil.h util.h strings.h string.h sys/stat.h )


dnl Checks for typedefs, structures, and compiler characteristics.
AC_LANG_CPLUSPLUS
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
AC_LANG_C
AC_C_LONG_DOUBLE
AC_TYPE_GETGROUPS

dnl for libsmb
AC_CHECK_SIZEOF(char,1)
AC_CHECK_SIZEOF(short,2)
AC_CHECK_SIZEOF(int,4)
AC_CHECK_SIZEOF(long,4)
AC_C_BIGENDIAN

AC_CHECK_FUNCS(socket powl sqrtl strdup getdtablesize vsnprintf setpgid nice seteuid snprintf vsyslog initgroups setgroups getgroups grantpt pw_encrypt setpriority getpt unlockpt ptsname setenv unsetenv waitpid)
AC_CHECK_GETDOMAINNAME
AC_CHECK_GETHOSTNAME
AC_CHECK_USLEEP
AC_CHECK_RANDOM
AC_CHECK_S_ISSOCK
AC_CHECK_KDEMAXPATHLEN

dnl Perform program name transformation
AC_ARG_PROGRAM

AC_DEFINE_UNQUOTED(KDEDIR, "$prefix", [The prefix to use as fallback])

CXXFLAGS="$CXXFLAGS -DQT_CLEAN_NAMESPACE -DQT_NO_COMPAT -DQT_NO_ASCII_CAST"
dnl ------------------------------------------------------------------------
dnl Try to find the SSL headers and libraries.
dnl $(SSL_LDFLAGS) will be -Lsslliblocation (if needed)
dnl and $(SSL_INCLUDES) will be -Isslhdrlocation (if needed)
dnl ------------------------------------------------------------------------
dnl
AC_DEFUN(AC_PATH_SSL,
[
LIBSSL="-lssl -lcrypto"
AC_MSG_CHECKING([for OpenSSL])

ac_ssl_includes=NO ac_ssl_libraries=NO
ssl_libraries=""
ssl_includes=""
AC_ARG_WITH(ssl-dir,
    [  --with-ssl-dir=DIR      where the root of OpenSSL is installed],
    [  ac_ssl_includes="$withval"/include
       ac_ssl_libraries="$withval"/lib
    ])

want_ssl=yes
AC_ARG_WITH(ssl,
    [  --without-ssl           disable SSL checks],
    [want_ssl=$withval])

if test $want_ssl = yes; then

AC_CACHE_VAL(ac_cv_have_ssl,
[#try to guess OpenSSL locations

  ssl_incdirs="/usr/include /usr/local/include /usr/ssl/include /usr/local/ssl/include"
  ssl_incdirs="$ac_ssl_includes $ssl_incdirs"
  AC_FIND_FILE(openssl/ssl.h, $ssl_incdirs, ssl_incdir)
  ac_ssl_includes="$ssl_incdir"

  ssl_libdirs="/usr/lib /usr/local/lib /usr/ssl/lib /usr/local/ssl/lib"
  if test ! "$ac_ssl_libraries" = "NO"; then
    ssl_libdirs="$ac_ssl_libraries $ssl_libdirs"
  fi

  test=NONE
  ssl_libdir=NONE
  for dir in $ssl_libdirs; do
    try="ls -1 $dir/libssl*"
    if test=`eval $try 2> /dev/null`; then ssl_libdir=$dir; break; else echo "tried $dir" >&AC_FD_CC ; fi
  done

  ac_ssl_libraries="$ssl_libdir"

  AC_LANG_SAVE
  AC_LANG_C

  ac_cflags_safe="$CFLAGS"
  ac_ldflags_safe="$LDFLAGS"
  ac_libs_safe="$LIBS"

  CFLAGS="$CFLAGS -I$ssl_incdir $all_includes"
  LDFLAGS="-L$ssl_libdir $all_libraries"
  LIBS="$LIBS $LIBSSL -lRSAglue -lrsaref"

  AC_TRY_LINK(,void RSAPrivateEncrypt(void);RSAPrivateEncrypt();,
  ac_ssl_rsaref="yes"
  ,
  ac_ssl_rsaref="no"
  )

  CFLAGS="$ac_cflags_safe"
  LDFLAGS="$ac_ldflags_safe"
  LIBS="$ac_libs_safe"

  AC_LANG_RESTORE

  if test "$ac_ssl_includes" = NO || test "$ac_ssl_libraries" = NO; then
    have_ssl=no
  else
    have_ssl=yes;
  fi

  ])

  eval "$ac_cv_have_ssl"
else
  have_ssl=no
fi

if test "$have_ssl" = yes; then
  dnl Check for SSL version
  AC_CACHE_VAL(ac_cv_ssl_version,
  [
    AC_LANG_SAVE
    AC_LANG_C 

    cat >conftest.$ac_ext <<EOF
#include <openssl/opensslv.h>
#include <stdio.h>
    int main() {
 
#ifndef OPENSSL_VERSION_NUMBER
      printf("ssl_version=\\"error\\"\n");
#else
      if (OPENSSL_VERSION_NUMBER < 0x00905000)
        printf("ssl_version=\\"old\\"\n");
      else if (OPENSSL_VERSION_NUMBER < 0x00906000)
        printf("ssl_version=\\"0.9.5\\"\n");
      else
        printf("ssl_version=\\"ok\\"\n");
#endif
     return (0);
    }
EOF

    ac_compile='${CC-gcc} $CFLAGS -I$ac_ssl_includes conftest.$ac_ext -o conftest'
    if AC_TRY_EVAL(ac_compile); then 

      if eval `./conftest 2>&5`; then
        if test $ssl_version = error; then
          AC_MSG_ERROR([$ssl_incdir/openssl/opensslv.h doesn't define OPENSSL_VERSION_NUMBER !])
        else
          if test $ssl_version = old; then
            AC_MSG_WARN([OpenSSL version too old. Upgrade to 0.9.5a at least, see http://www.openssl.org. SSL support disabled.])
            have_ssl=no
          fi
        fi
        ac_cv_ssl_version="ssl_version=$ssl_version"
      else
        AC_MSG_ERROR([Your system couldn't run a small SSL test program.
        Check config.log, and if you can't figure it out, send a mail to 
        David Faure <faure@kde.org>, attaching your config.log])
      fi

    else
      AC_MSG_ERROR([Your system couldn't link a small SSL test program.
      Check config.log, and if you can't figure it out, send a mail to 
      David Faure <faure@kde.org>, attaching your config.log])
    fi 

    AC_LANG_RESTORE

  ])

  eval "$ac_cv_ssl_version"
fi

if test "$have_ssl" != yes; then
  LIBSSL="";
  AC_MSG_RESULT([$have_ssl]);
else
  AC_DEFINE(HAVE_SSL, 1, [If we are going to use OpenSSL])
  ac_cv_have_ssl="have_ssl=yes \
    ac_ssl_includes=$ac_ssl_includes ac_ssl_libraries=$ac_ssl_libraries ac_ssl_rsaref=$ac_ssl_rsaref"
  AC_MSG_RESULT([libraries $ac_ssl_libraries, headers $ac_ssl_includes])
  
  ssl_libraries="$ac_ssl_libraries"
  ssl_includes="$ac_ssl_includes"

  AC_MSG_CHECKING([whether OpenSSL uses rsaref])
  AC_MSG_RESULT($ac_ssl_rsaref)
  if test "$ac_ssl_rsaref" = yes; then
    LIBSSL="-lssl -lcrypto -lRSAglue -lrsaref" 
  fi

  AC_MSG_CHECKING([for OpenSSL version])
  AC_MSG_RESULT($ssl_version)
  if test $ssl_version = "0.9.5"; then
    AC_DEFINE(HAVE_OLD_SSL_API, 1, [Define if you have OpenSSL < 0.9.6])
  fi
fi

if test "$ssl_includes" = "/usr/include" || test  "$ssl_includes" = "/usr/local/include" || test -z "$ssl_includes"; then
 SSL_INCLUDES="";
else
 SSL_INCLUDES="-I$ssl_includes"
fi

if test "$ssl_libraries" = "/usr/lib" || test "$ssl_libraries" = "/usr/local/lib" || test -z "$ssl_libraries"; then
 SSL_LDFLAGS=""
else
 SSL_LDFLAGS="-L$ssl_libraries"
fi

AC_SUBST(SSL_INCLUDES)
AC_SUBST(SSL_LDFLAGS)
AC_SUBST(LIBSSL)
])


AC_PATH_SSL

dnl don't remove
dnl AC_OUTPUT(khtml/java/kjava.policy)
dnl KDE JavaScript specific configure tests

AC_CHECK_HEADERS(ieeefp.h)
AC_CHECK_LIB(m, isinf, [
  AC_DEFINE_UNQUOTED(HAVE_FUNC_ISINF, 1, [Define if you have isinf])
])
AC_CHECK_LIB(m, finite, [
  AC_DEFINE_UNQUOTED(HAVE_FUNC_FINITE, 1, [Define if you have finite])
])
AC_CHECK_LIB(m, isnan, [
  AC_DEFINE_UNQUOTED(HAVE_FUNC_ISNAN, 1, [Define if you have isnan])
])

AC_DEFUN(AC_CHECK_PCREPOSIX,
[
  AC_MSG_CHECKING([for pcreposix])
  AC_CACHE_VAL(ac_cv_have_pcreposix,
  [
    ac_save_libs="$LIBS"
    LIBS="-lpcre -lpcreposix"
    AC_TRY_LINK(
      [#include <pcreposix.h>],
      [regfree(0);],
      [ac_cv_have_pcreposix="yes"],
      [ac_cv_have_pcreposix="no"]
    )
    LIBS="$ac_save_libs"
  ])
  AC_MSG_RESULT($ac_cv_have_pcreposix)
  if test "$ac_cv_have_pcreposix" = "yes"; then
    LIBPCRE="-lpcre -lpcreposix"
    AC_DEFINE(HAVE_PCREPOSIX, 1, [Define if you have pcreposix libraries and header files.])
  fi
])
AC_CHECK_PCREPOSIX
AC_SUBST(LIBPCRE)
KDE_CREATE_SUBDIRSLIST
AC_OUTPUT( \
./Makefile \
konq-embed/Makefile \
konq-embed/dropin/Makefile \
konq-embed/dropin/atheos/Makefile \
konq-embed/dropin/bak/kio/Makefile \
konq-embed/dropin/bak/kssl/Makefile \
konq-embed/dropin/khtml/Makefile \
konq-embed/dropin/khtml/java/Makefile \
konq-embed/dropin/kio/Makefile \
konq-embed/dropin/kparts/Makefile \
konq-embed/kdesrc/Makefile \
konq-embed/kdesrc/bak/kio/Makefile \
konq-embed/kdesrc/bak/kio/http/Makefile \
konq-embed/kdesrc/bak/kio/http/kcookiejar/Makefile \
konq-embed/kdesrc/bak/kssl/Makefile \
konq-embed/kdesrc/bak/kssl/kssl/Makefile \
konq-embed/kdesrc/bak/kssl/kssl/caroot/Makefile \
konq-embed/kdesrc/bak/kssl/kssl/caroot/expired/Makefile \
konq-embed/kdesrc/kdecore/Makefile \
konq-embed/kdesrc/khtml/Makefile \
konq-embed/kdesrc/khtml/css/Makefile \
konq-embed/kdesrc/khtml/dom/Makefile \
konq-embed/kdesrc/khtml/ecma/Makefile \
konq-embed/kdesrc/khtml/html/Makefile \
konq-embed/kdesrc/khtml/java/Makefile \
konq-embed/kdesrc/khtml/java/tests/Makefile \
konq-embed/kdesrc/khtml/misc/Makefile \
konq-embed/kdesrc/khtml/pics/Makefile \
konq-embed/kdesrc/khtml/rendering/Makefile \
konq-embed/kdesrc/khtml/xml/Makefile \
konq-embed/kdesrc/kjs/Makefile \
konq-embed/kdesrc/kparts/Makefile \
konq-embed/src/Makefile \
khtml/java/kjava.policy
)
